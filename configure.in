AC_INIT(configure.in)
AM_INIT_AUTOMAKE(libuser,0.21)

CFLAGS="$CFLAGS -D_GNU_SOURCE"

AC_PROG_CC
AC_ISC_POSIX
AC_PROG_LN_S
AC_PATH_PROG(MAKE,make)
AC_PROG_MAKE_SET
AC_PROG_AWK

AM_PROG_LIBTOOL

AC_CHECK_LIB(crypt,crypt)
AM_PATH_GLIB(1.2.9,,,gmodule)

ALL_LINGUAS="no"
AM_GNU_GETTEXT

popt=/usr
AC_ARG_WITH(popt,
[  --without-popt=DIR       Use popt headers and libraries under DIR],
[
if test x$withval != x -a x$withval != xyes ; then
  popt=$withval
fi
])
if test x$popt != xno ; then
	if test x$popt != x -a x$popt != x/usr ; then
		CFLAGS="$CFLAGS -I$popt/include"
		CPPFLAGS="$CPPFLAGS -I$popt/include"
		LIBS="$LIBS -L$popt/lib"
	fi
	AC_CHECK_HEADERS(popt.h)
	AC_CHECK_LIB(popt,poptGetContext)
fi

MODULES="$MODULES ${PACKAGE}_files.so"
MODULES="$MODULES ${PACKAGE}_shadow.so"

AC_ARG_WITH(krb5,
[  --with-krb5=DIR       Use Kerberos 5 headers and libraries under DIR],
[
if test x$withval != x -a x$withval != xno ; then
	krb5=$withval
else
	krb5=no
fi
],krb5=no)
if test x$krb5 != xno ; then
	LIBSAVE="$LIBS"
	if test x$krb5 != xyes -a x$krb5 != x/usr ; then
		CFLAGS="$CFLAGS -I$krb5/include"
		CPPFLAGS="$CPPFLAGS -I$krb5/include"
		LDFLAGS="$LDFLAGS -L$krb5/lib"
		LIBS="$LIBS -L$krb5/lib"
	fi
	AC_CHECK_HEADERS(krb5.h)
	AC_CHECK_FUNC(error_message,,
		AC_CHECK_LIB(com_err,error_message,
			     KRB5_LIBS="-lcom_err $KRB5_LIBS"
			     LIBS="-lcom_err $LIBS",,$LDFLAGS $LIBS))
	AC_CHECK_FUNC(mit_des_key_sched,,
		AC_CHECK_LIB(crypto,mit_des_key_sched,
			     KRB5_LIBS="-lcrypto $KRB5_LIBS"
			     LIBS="-lcrypto $LIBS"
			,AC_CHECK_LIB(k5crypto,mit_des_key_sched,
				      KRB5_LIBS="-lk5crypto $KRB5_LIBS"
				      LIBS="-lk5crypto $LIBS",,$LDFLAGS $LIBS)))
	AC_CHECK_FUNC(krb5_init_context,,
		AC_CHECK_LIB(krb5,krb5_init_context,
			     KRB5_LIBS="-lkrb5 $KRB5_LIBS"
			     LIBS="-lkrb5 $LIBS",,$LDFLAGS $LIBS))
	AC_CHECK_FUNC(kadm5_chpass_principal,,
		AC_CHECK_LIB(kadm5clnt,kadm5_chpass_principal,
			     KRB5_LIBS="-lkadm5clnt $KRB5_LIBS"
			     LIBS="-lkadm5clnt $LIBS",,$LDFLAGS $LIBS))
	AC_CHECK_FUNC(kadm5_create_principal,
		AC_CHECK_FUNC(kadm5_delete_principal,
			AC_CHECK_FUNC(kadm5_modify_principal,
				AC_CHECK_FUNC(kadm5_rename_principal,
					MODULES="$MODULES ${PACKAGE}_krb5.so"))))
	LIBS="$LIBSAVE"
fi

AC_ARG_WITH(ldap,
[  --with-ldap=DIR       Use OpenLDAP 2.x headers and libraries under DIR],
[
if test x$withval != x -a x$withval != xno ; then
	ldap=$withval
else
	ldap=no
fi
],ldap=no)
if test x$ldap != xno ; then
	LIBSAVE="$LIBS"
	if test x$ldap != xyes -a x$ldap != x/usr ; then
		CFLAGS="$CFLAGS -I$ldap/include"
		CPPFLAGS="$CPPFLAGS -I$ldap/include"
		LDFLAGS="$LDFLAGS -L$ldap/lib"
	fi
	AC_CHECK_HEADERS(ldap.h)
	AC_CHECK_FUNC(ber_alloc,,
		AC_CHECK_LIB(lber,ber_alloc,
			     LDAP_LIBS="-llber $LDAP_LIBS"
			     LIBS="-llber $LIBS",,$LDFLAGS $LIBS))
	AC_CHECK_FUNC(ldap_sasl_bind_s,,
		AC_CHECK_LIB(ldap,ldap_sasl_bind_s,
			     LDAP_LIBS="-lldap $LDAP_LIBS"
			     LIBS="-lldap $LIBS",,$LDFLAGS $LIBS))
	AC_CHECK_FUNC(ldap_sasl_interactive_bind_s,
		AC_CHECK_FUNC(ldap_set_option,
			AC_CHECK_FUNC(ldap_start_tls_s,
				AC_CHECK_FUNC(ldap_search_s,
					AC_CHECK_FUNC(ldap_get_values,
						AC_CHECK_FUNC(ldap_modify_ext_s,
							AC_CHECK_FUNC(ldap_delete_ext_s,
								MODULES="$MODULES ${PACKAGE}_ldap.so")))))))
	LIBS="$LIBSAVE"
fi

AC_ARG_WITH(sasl,
[  --with-sasl=DIR       Use Cyrus SASL headers and libraries under DIR],
[
if test x$withval != x -a x$withval != xno ; then
	sasl=$withval
else
	sasl=no
fi
],sasl=no)
if test x$sasl != xno ; then
	LIBSAVE="$LIBS"
	if test x$sasl != xyes -a x$sasl != x/usr ; then
		CFLAGS="$CFLAGS -I$sasl/include"
		CPPFLAGS="$CPPFLAGS -I$sasl/include"
		LDFLAGS="$LDFLAGS -L$sasl/lib"
		LIBS="$LIBS -L$sasl/lib"
	fi
	AC_CHECK_HEADERS(sasl.h)
	AC_CHECK_FUNC(sasl_setpass,,
		AC_CHECK_LIB(sasl,sasl_setpass,
			     SASL_LIBS="-lsasl $SASL_LIBS"
			     LIBS="-lsasl $LIBS"
			     MODULES="$MODULES ${PACKAGE}_sasldb.so"
			     ,,$LDFLAGS $LIBS))
	AC_CHECK_FUNCS(sasl_user_exists)
	LIBS="$LIBSAVE"
fi

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/time.h unistd.h)

AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

AC_CHECK_FUNCS(strstr strtol)

AC_SUBST(MODULES)
AC_SUBST(KRB5_LIBS)
AC_SUBST(LDAP_LIBS)
AC_SUBST(SASL_LIBS)

if test `eval echo x$libdir` != xNONE/lib
then
	pkglibdir=`eval echo ${libdir}/${PACKAGE}`
elif test `eval echo x$exec_prefix` != xNONE
then
	pkglibdir=`eval echo ${exec_prefix}/lib/${PACKAGE}`
elif test `eval echo x$prefix` != xNONE
then
	pkglibdir=`eval echo ${prefix}/lib/${PACKAGE}`
else
	pkglibdir=`eval echo /usr/local/lib/${PACKAGE}`
fi
scdir=`eval echo $sysconfdir`
AC_SUBST(pkglibdir)
AC_SUBST(scdir)

AM_CONFIG_HEADER(config.h)

AC_OUTPUT(
Makefile
intl/Makefile
po/Makefile.in
include/Makefile
include/libuser/Makefile
lib/Makefile
quota/Makefile
modules/Makefile
python/Makefile
samples/Makefile
apps/Makefile
docs/Makefile
docs/sgml/Makefile
libuser.spec
libuser.conf
)

$MAKE -C po -f Makefile.in Makefile
